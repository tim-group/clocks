subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'com.timgroup.jarmangit'

    group = 'com.timgroup'
    if (System.getenv("BUILD_NUMBER")) version = "1.0.${System.getenv('BUILD_NUMBER')}"
}

ext {
    jodaTimeVersion = '2.9.9'
}

subprojects {
    jar {
        manifest {
            attributes(
                    'Implementation-Title': project.name,
                    'Implementation-Version': project.version,
                    'Implementation-Vendor': 'TIM Group Ltd'
            )
        }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
        options.encoding = "UTF-8"
        options.incremental = true
        options.deprecation = true
        options.compilerArgs << "-parameters"
    }

    repositories {
        maven {
            name "nexus"
            url "${project.repoUrl}/groups/public"
        }
    }

    apply plugin: 'maven-publish'

    publishing {
        repositories {
            maven {
                name "nexus"
                url "${project.repoUrl}/groups/public"
            }
            maven {
                name "localFiles"
                url "${rootProject.buildDir}/repo"
            }
        }
        publications {
            mavenJava(MavenPublication) {
                artifactId project.ext.has('artifactId') ? project.ext.artifactId
                        : project == rootProject ? project.name
                        : rootProject.name + project.path.replace(':', '-')
                from components.java
                artifact(sourcesJar)
                artifact(javadocJar)
                pom.withXml {
                    asNode().children().last() + {
                        resolveStrategy = DELEGATE_FIRST
                        name project.ext.has('pomName') ? project.ext.pomName : project.name
                        description project.description
                        url "http://github.com/tim-group/clocks"
                        licenses {
                            license {
                                name 'The BSD 2-Clause License'
                                url 'http://opensource.org/licenses/BSD-2-Clause'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'steve.haslam@timgroup.com'
                                name 'Steve Haslam'
                                email 'steve.haslam@timgroup.com'
                            }
                        }
                    }
                }
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    assemble.dependsOn(sourcesJar)

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    assemble.dependsOn(javadocJar)
}


buildscript {
    repositories {
        maven {
            url "${project.repoUrl}/groups/public"
        }
    }

    dependencies {
        classpath "com.timgroup:gradle-jarmangit:1.1.+"
    }
}
