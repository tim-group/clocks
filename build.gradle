subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'autobumper'
    apply plugin: 'jarmangit'

    group = 'com.timgroup'
    if (System.getenv('BUILD_NUMBER') != null) {
        version = "1.0." + System.getenv('BUILD_NUMBER')
    }
}

ext {
    jodaTimeVersion = '2.9.9'
}

subprojects {
    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
        options.encoding = "UTF-8"
        options.incremental = true
        options.deprecation = true
        options.compilerArgs << "-parameters"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    repositories {
        if (project.hasProperty('repoUrl')) {
            maven {
                name "nexus"
                url "${project.repoUrl}/groups/public"
            }
        } else {
            mavenCentral()
        }
    }

    apply plugin: 'maven'

    uploadArchives {
        if (project.hasProperty('repoUrl')) {
            repositories {
                mavenDeployer {
                    repository(url: "${project.repoUrl}/repositories/yd-release-candidates") {
                        authentication(userName: project.repoUsername, password: project.repoPassword)
                    }
                }
            }
        }
    }
}

buildscript {
    repositories {
        if (project.hasProperty('repoUrl')) {
            maven {
                url "${project.repoUrl}/groups/public"
            }
        } else {
            mavenCentral()
        }
    }

    dependencies {
        classpath "com.timgroup:gradle-autobumper:1.0.+"
        classpath "com.timgroup:gradle-jarmangit:1.0.+"
    }
}
